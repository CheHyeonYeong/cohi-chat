plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.coDevs'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

bootRun {
    // 애플리케이션 실행 시 JVM 인코딩을 UTF-8로 설정
    jvmArgs "-Dfile.encoding=UTF-8", "-Dsun.stdout.encoding=UTF-8", "-Dsun.stderr.encoding=UTF-8"
    // 로컬 개발 시 local 프로파일 적용 (application-local.properties 로드)
    systemProperty "spring.profiles.active", "local"
    // 프로젝트 루트의 .env 파일 로드
    def envFile = file("${projectDir}/../.env")
    if (envFile.exists()) {
        envFile.readLines().each { line ->
            if (line && !line.startsWith('#') && line.contains('=')) {
                def parts = line.split('=', 2)
                environment(parts[0].trim(), parts[1]?.trim() ?: '')
            }
        }
    }
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom 'software.amazon.awssdk:bom:2.29.51'
	}
}

dependencies {

    // AWS SDK v2 - S3
    implementation 'software.amazon.awssdk:s3'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Bucket4j Rate Limiting
    implementation 'com.bucket4j:bucket4j_jdk17-core:8.14.0'
    implementation 'com.bucket4j:bucket4j_jdk17-redis-common:8.14.0'
    implementation 'com.bucket4j:bucket4j_jdk17-lettuce:8.14.0'

    runtimeOnly 'org.xerial:sqlite-jdbc:3.45.2.0'
    implementation 'org.hibernate.orm:hibernate-community-dialects'

    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4'
    
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.github.codemonstur:embedded-redis:1.4.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.apache.commons:commons-text:1.15.0'

    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Google Calendar API
    implementation 'com.google.api-client:google-api-client:2.7.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.36.0'
    implementation 'com.google.apis:google-api-services-calendar:v3-rev20241101-2.0.0'
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.29.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-params'
}

tasks.named('test') {
	useJUnitPlatform()
}
